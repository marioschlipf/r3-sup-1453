plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'io.spring.dependency-management' version '1.0.7.RELEASE'
    id 'net.corda.plugins.cordformation'
}

ext {
    spring_boot_version = '2.3.4.RELEASE'
    spring_version = '5.2.9.RELEASE'
}

sourceSets {
    main {
        resources {
            srcDir "config/dev"
        }
    }
}

// See https://docs.spring.io/dependency-management-plugin/docs/current/reference/html
dependencyManagement {
    dependencies {
        dependency "org.apache.logging.log4j:log4j-slf4j-impl:$log4j_version"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    compile "$corda_core_release_group:corda-core:$corda_release_version"
    compile "$corda_release_group:corda-jackson:$corda_release_version"
    compile "$corda_release_group:corda-rpc:$corda_release_version"

    compile("org.springframework.boot:spring-boot-starter-websocket:$spring_boot_version") {
        exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
    }
    compile "org.springframework.boot:spring-boot-starter-log4j2:$spring_boot_version"
    compile "org.apache.logging.log4j:log4j-web:$log4j_version"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
        languageVersion = "1.2"
        apiVersion = "1.2"
        jvmTarget = "1.8"
        javaParameters = true   // Useful for reflection.
    }
}